import win.ui;
import console;
import win.ui.atom;
import web.form;
import web.mshtml;
import web.util;
import inet.http;
import fsys;
import fsys.table;
import string.html;
import sqlite;
/*DSG{{*/
mainForm = win.form(text="aardio form";right=928;bottom=814)
mainForm.add(
button={cls="button";text="获取交易记录";left=631;top=435;right=900;bottom=489;z=4};
custom={cls="custom";text="custom";left=14;top=12;right=900;bottom=420;autosize=1;center=1;edge=1;transparent=1;z=3};
edit={cls="edit";left=111;top=582;right=143;bottom=605;edge=1;num=1;z=6};
groupbox={cls="groupbox";text="异步通知";left=14;top=430;right=612;bottom=624;edge=1;z=2};
listview={cls="listview";left=14;top=12;right=900;bottom=420;edge=1;fullRow=1;gridLines=1;z=3};
static={cls="static";text="刷新时间：";left=47;top=586;right=112;bottom=603;transparent=1;z=5};
static2={cls="static";text="秒";left=151;top=586;right=191;bottom=603;transparent=1;z=7}
)
/*}}*/

import config;
config.mainForm.load();

mainForm.edit.setParent(mainForm.groupbox);
mainForm.edit.oncommand = function(id,event){
	//mainForm.msgbox( mainForm.edit.text );
	config.mainForm.save();
}
mainForm.custom.oncommand = function(id,event){
	//mainForm.msgbox( mainForm.custom.text );
	
}

var wb = web.form( mainForm.custom ); 

mainForm.listview.show(false);

wb.go("https://auth.alipay.com/login/index.htm");
wb.wait(""); //等待指定网址

wb.go("https://consumeprod.alipay.com:443/record/index.htm");

//按钮事件执行函数
mainForm.button.oncommand = function(id,event){
	if(mainForm.edit.text!=""){
		setTradeStatus();
		run();
		var tmId = mainForm.addtimer( 				//创建自动刷新函数
			(tonumber(mainForm.edit.text))*1000,	//获取edit控件中的值进行转化成number，这里是毫秒为单位，所以乘上1000
			function(hwnd,msg,id,tick){
				wb.go("https://consumeprod.alipay.com:443/record/index.htm");
				wb.wait("");
				setTradeStatus();
				run();
			} 
		)
	} else {
		mainForm.msgbox("请设置刷新时间！")
	}
	
}

mainForm.groupbox.oncommand = function(id,event){
	//mainForm.msgbox( mainForm.groupbox.text );
	
}

//限制交易记录条件（交易状态，资金流向，交易分类）
function setTradeStatus(){
	var tradeStatus = wb.jQuery("li[data-value='success']");
	tradeStatus[0].click();
	var fundFlow = wb.jQuery("li[data-value='in']");
	fundFlow[0].click();
	var TradeType = wb.jQuery("li[data-value='TRANSFER']");
	TradeType[0].click();

	
	var setQueryForm = wb.getEle("J-set-query-form");
	setQueryForm.click();
}

function run(){
	wb.wait(""); //等待指定网址
	
	var tradesTable = {};
	getTradesTable(tradesTable);
	mainForm.listview.clear();
	
	var sqlConnection = sqlite("/data.db");
	
	if( not sqlConnection.existsTable("tradedata") ){
		sqlConnection.exec("create table tradedata(
			tradeNum TEXT NOT NULL UNIQUE,
			cTime TEXT NOT NULL,
			tradeName TEXT NOT NULL,
			theOtherSide TEXT NOT NULL,
			money TEXT NOT NULL,
			status TEXT NOT NULL,
			result TEXT
		);");
	}
	
	for k,v in tradesTable  { 
	//k为键,v是匹配的值,在这里键值对无序的随机出现。
		cTime, tradeName, tradeNum, theOtherSide, money, status = table.unpack(v);//获取table中列数据
		
		//这里去除一些不必要的字符,注意点：table加入只有数字的字符串时会自动转换成number类型
		theOtherSide = string.replace(theOtherSide,"@<span>","");
		theOtherSide = string.replace(theOtherSide,"@</span>","");
		money = string.replace(money,"@+","");
		tradeNum = string.replace(tradeNum,"@流水号:","");
		
		mainForm.listview.addItem({cTime; tradeName; tradeNum; theOtherSide; money; status});//将数据加载到列表视图
		sqlConnection.exec("INSERT OR IGNORE INTO tradedata(tradeNum,cTime,tradeName,theOtherSide,money,status) VALUES ('%s','%s','%s','%s','%s','%s');",tradeNum,cTime,tradeName,theOtherSide,money,status);	
	};
	
	mainForm.custom.show(false);
	mainForm.listview.show(true);
}

//获取交易记录数据
function getTradesTable(tradesTable){
	var oTable = wb.getEle("tradeRecordsIndex");	//获取指定列表
	var aTr = oTable.getElementsByTagName("tr");	//当前页的总tr元素
	for( i=1;aTr.length-1;1) {						//循环tr元素
		var aTd = aTr[i].getElementsByTagName("td");	//获取当前tr的总td元素
		var oTradeCell = {};							//创建table类型局部变量，存储当前列的数据
		for (j=0;aTd.length-1;1) {						//循环当前tr的td元素
			//mainForm.msgbox();
			if(j!=1 && j!=6 && j!=8){					//去除memo列、detail列、action列
				var oCell = aTd[j];
				var content = "";
				var oCellChild = oCell.firstChild;		//获取当前行的当前列的子节点
				for (k=0;oCell.childNodes.length-1;1) {
					if(oCellChild.innerHTML && string.trim(oCellChild.innerHTML)!=""){
						content += string.trim(oCellChild.innerHTML) + " ";
					}
					if(oCellChild.nextSibling){
						oCellChild = oCellChild.nextSibling;
					}
				}
				table.push(oTradeCell, content);		//将当前列的数据储存到oTradeCell
			}
		}
		table.push(tradesTable,oTradeCell);		//将当前行的数据储存到tradesTable
	}
	var nextPage = wb.queryEles( className="page-next" );
	if(nextPage.hasChildNodes){
		nextPage.click();
		wb.wait("");
		getTradesTable(tradesTable);
	}
}

//创建列名
mainForm.listview.insertColumn("创建时间",150,,0x0/*_LVCFMT_LEFT*/);
mainForm.listview.insertColumn("交易名称",150,,0x0/*_LVCFMT_LEFT*/);
mainForm.listview.insertColumn("交易号",300,,0x0/*_LVCFMT_LEFT*/);
mainForm.listview.insertColumn("对方",100,,0x0/*_LVCFMT_LEFT*/);
mainForm.listview.insertColumn("金额",100,,0x0/*_LVCFMT_LEFT*/);
mainForm.listview.insertColumn("状态",100,,0x0/*_LVCFMT_LEFT*/);
mainForm.listview.onnotify = function(id,code,ptr){
	
}

mainForm.show() 
return win.loopMessage(); 
